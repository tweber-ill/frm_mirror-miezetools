/*
 * Coil Test
 * @author tweber
 * @date 3-nov-14
 */

DEFINE INSTRUMENT CoilTest
(
	double dLam = 5.0,
	double dDeltaLam = 0.5,

	double dL1 = 1.0,
	double dL2 = 2.0,

	double dOm0 = 45000.0,
	double dOm1 = 64000.0
)

DECLARE
%{
	#include "../neutrons.h"

	double dVel;
	double dB_rf0, dB_rf1;
	
	double dB00, dB01;

	const double dCoilLen = 0.005;
	const double dDistCoil = 1.0;

	double dfM;
	double dDetT0, dDetT1;


	int coil0_field(double x, double y, double z, double t, 
			double *pBx, double *pBy, double *pBz, 
			void *pData)
	{
		//printf("x=%g, y=%g, z=%g, t=%g\n", x,y,z,t);
		//t -= dDistCoil/dVel;
		rf_flipper_rot(x,y,z,t, dOm0,dB_rf0,dB00, pBx,pBy,pBz);

		return 1;
	}

	int coil1_field(double x, double y, double z, double t, 
			double *pBx, double *pBy, double *pBz, 
			void *pData)
	{
		//printf("x=%g, y=%g, z=%g, t=%g\n", x,y,z,t);
		//t -= dDistCoil/dVel;
		rf_flipper_rot(x,y,z,t, dOm1,dB_rf1,dB01, pBx,pBy,pBz);

		return 1;
	}
%}



INITIALIZE
%{
	dVel = neutron_v(dLam);
	dB_rf0 = larmor_B(dVel, dCoilLen, -M_PI);
	dB_rf1 = larmor_B(dVel, dCoilLen, -M_PI);

	dB00 = dOm0 / GAMMA_N;
	dB01 = dOm1 / GAMMA_N;

	dfM = 2.*(dOm1-dOm0)/(2.*M_PI);

	printf("lam=%g, v=%g\n", dLam, dVel);
	printf("L1=%g, L2=%g, fM=%g\n", dL1, dL2, dfM);
	printf("coil 0: B_0=%g, B_rf=%g, om=%g\n", dB00, dB_rf0, dOm0);
	printf("coil 1: B_0=%g, B_rf=%g, om=%g\n", dB01, dB_rf1, dOm1);

	double dDistDet = dDistCoil + dL1 + dCoilLen + dL2;
	dDetT0 = dDistDet/dVel;
	dDetT1 = dDetT0 + 1./dfM * 4.;
%}






TRACE

COMPONENT Origin = Progress_bar()
	 	AT (0,0,0) ABSOLUTE



//----------------------------------------------------------------------------------------------------
// source

COMPONENT Source = Source_simple(
	yheight = 0.01, xwidth = 0.01, 
	focus_xw = 0.01, focus_yh = 0.01, dist = 0.5,
	lambda0 = dLam, dlambda = dDeltaLam, gauss = 1)
		AT (0, 0, 0) RELATIVE Origin
		EXTEND
		%{
			//t = rand01()*1./(mcipdOm1-mcipdOm0)*20.;
			t = rand01() * (dDetT1-dDetT0);

			// polarised
			sx = 1.;
			sy = 0.;
			sz = 0.;
		%}
//----------------------------------------------------------------------------------------------------





//----------------------------------------------------------------------------------------------------
// coils

COMPONENT coil0 = Pol_simpleBfield(
	fieldFunction = coil0_field, Bx=0, By=0, Bz=0, filename = 0,
	xwidth = 0.05, yheight = 0.05, zdepth=dCoilLen)
		AT (0.0, 0.0, dDistCoil) RELATIVE Origin
COMPONENT coil0_stop = Pol_simpleBfield_stop(
		magnet_comp_stop=coil0)
		AT (0.0, 0.0, dDistCoil+dCoilLen)  RELATIVE Origin

COMPONENT coil1 = Pol_simpleBfield(
	fieldFunction = coil1_field, Bx=0, By=0, Bz=0, filename = 0,
	xwidth = 0.05, yheight = 0.05, zdepth=dCoilLen)
		AT (0.0, 0.0, dDistCoil+dL1) RELATIVE Origin
COMPONENT coil1_stop = Pol_simpleBfield_stop(
		magnet_comp_stop=coil1)
		AT (0.0, 0.0, dDistCoil+dL1+dCoilLen)  RELATIVE Origin
		EXTEND
		%{
			p *= 0.5 * (1. + sx);
		%}


//----------------------------------------------------------------------------------------------------
// detectors

COMPONENT pollam_x = PolLambda_monitor(
	nL = 128, npol = 128, restore_neutron = 1, filename = "pollam_x",
	mx = 1.0, my = 0.0, mz = 0.0,
	Lmin = dLam-2*dDeltaLam, Lmax = dLam+2*dDeltaLam,
	xwidth = 0.04, yheight = 0.04)
		AT (0.0, 0.0, dL2) RELATIVE coil1_stop
COMPONENT pollam_y = PolLambda_monitor(
	nL = 128, npol = 128, restore_neutron = 1, filename = "pollam_y",
	mx = 0.0, my = 1.0, mz = 0.0,
	Lmin = dLam-2*dDeltaLam, Lmax = dLam+2*dDeltaLam,
	xwidth = 0.04, yheight = 0.04)
		AT (0.0, 0.0, dL2) RELATIVE coil1_stop
COMPONENT pollam_z = PolLambda_monitor(
	nL = 128, npol = 128, restore_neutron = 1, filename = "pollam_z",
	mx = 0.0, my = 0.0, mz = 1.0,
	Lmin = dLam-2*dDeltaLam, Lmax = dLam+2*dDeltaLam,
	xwidth = 0.04, yheight = 0.04)
		AT (0.0, 0.0, dL2) RELATIVE coil1_stop

COMPONENT tof = TOF_monitor(
	nt=128, tmin=dDetT0*1e6, tmax=dDetT1*1e6, //tmax=1.0/(64000.0-45000.0)*10.0 * 1e6,
	xwidth = 0.04, yheight = 0.04,
	filename = "tof", restore_neutron = 1)
		AT (0, 0, dL2) RELATIVE coil1_stop

COMPONENT psd = PSD_monitor(
	nx = 128, ny = 128, restore_neutron = 1, filename = "psd",
	xwidth = 0.04, yheight = 0.04)
		AT (0.0, 0.0, dL2) RELATIVE coil1_stop

//----------------------------------------------------------------------------------------------------


FINALLY
%{
%}


END
